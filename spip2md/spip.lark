// SPIP Markup grammar for Lark

start: _N* block ( _N+ block )+ _N*

?block: SEPARATOR -> hr
      | unordered_list
      | ordered_list
      | table
      | paragraph
      | heading

unordered_list: ( "-*" list_element _N )+ -> ul
ordered_list: ( "-#" list_element _N )+ -> ol
list_element: _inline_format -> li

table: ( row _N )+ -> table
row: ( "|" cell )+ "|" -> tr
cell: _inline_format -> td

heading: "{{{" ( link | nested_italic | nested_bold | TEXT ) "}}}" -> h2

paragraph: ( _inline_format _N? )+ -> p

_inline_format: bold
              | italic
              | link
              | TEXT

bold: "{{" ( link | nested_italic | TEXT )+ "}}" -> strong
italic: "{" ( link | nested_bold | TEXT )+ "}" -> em
nested_bold: TEXT _NOT_LEFT_BRACE "{{" ( link | TEXT )+ "}}" -> strong
nested_italic: TEXT _NOT_LEFT_BRACE "{" ( link | TEXT )+ "}" -> em

?link: a
     | footnote
     | wikipedia_link

a: "[" TEXT "->" link_destination "]" -> a
link_destination: TEXT -> href

footnote: "[[" TEXT "]]" -> footnote
wikipedia_link: "[?" TEXT "]" -> a_wikipedia

// Negative terminals

_NOT_LEFT_BRACE: /[^\{]/

// Terminals

SEPARATOR: "----" "-"*

// Windows or Unix line breaks
_N: /\r/? /\n/

// Pure text :
// - Never contains line breaks
// - Is the least priority element, so should be lazily matched
TEXT: /.+?/
