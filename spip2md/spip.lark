start: _N? block ( _N+ block )+ _N*

?block: heading
      | SEPARATOR -> hr
      | unordered_list
      | ordered_list
      | table
      | paragraph

heading: "{{{" ( TEXT | link | nested_italic | nested_bold ) "}}}" -> h2

SEPARATOR: "----" "-"*

unordered_list: ( "-*" list_element _N )+ -> ul
ordered_list: ( "-#" list_element _N )+ -> ol
list_element: _inline_format -> li

table: ( row _N )+ -> table
row: ( "|" cell )+ "|" -> tr
cell: _inline_format -> td

paragraph: ( _inline_format _N? )+ -> p

// Windows or Unix line break
_N: /\r/? /\n/

_inline_format: bold
              | italic
              | link
              | TEXT

bold: "{{" ( TEXT | link  | nested_italic )+ "}}" -> strong
italic: "{" ( TEXT | link | nested_bold )+ "}" -> em

nested_bold: _NOT_LBRACE "{{" ( TEXT | link ) "}}" _NOT_RBRACE -> strong
nested_italic: _NOT_LBRACE "{" ( TEXT | link ) "}" _NOT_RBRACE -> em

_NOT_LBRACE: /[^\{]/
_NOT_RBRACE: /[^\}]/

?link: internal_link
     | external_link
     | footnote
     | glossary

internal_link: "[" TEXT "->" TEXT "]"
external_link: "[" TEXT "->" _PROTOCOL "://" TEXT "]"

// Protocol, probably http(s)
// Is made of 2 to 8 latin letters
_PROTOCOL: /[a-zA-Z]{2,8}/

footnote: "[[" TEXT "]]"
glossary: "[?" TEXT "]"

// Pure text :
// - Never contains line breaks
// - Never contains curly braces
TEXT: /[^\r\n\{\}]/+
