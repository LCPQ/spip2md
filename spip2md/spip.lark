start: _N? block ( _N+ block )+ _N*

?block: heading
      | SEPARATOR -> hr
      | unordered_list
      | ordered_list
      | table
      | paragraph

heading: "{{{" ( TEXT | link | nested_italic | nested_bold ) "}}}" -> h2

SEPARATOR.9: "----" "-"*

unordered_list: ( "-*" list_element _N )+ -> ul
ordered_list: ( "-#" list_element _N )+ -> ol
list_element: _inline_format -> li

table: ( row _N )+ -> table
row: ( "|" cell )+ "|" -> tr
cell: _inline_format -> td

paragraph: ( _inline_format _N? )+ -> p

// Windows or Unix line break
_N: /\r/? /\n/

_inline_format: bold
              | italic
              | link
              | TEXT

bold: "{{" ( TEXT | link  | nested_italic )+ "}}" -> strong
italic: "{" ( TEXT | link | nested_bold )+ "}" -> em

nested_bold: _NOT_LBRACE "{{" ( TEXT | link ) "}}" _NOT_RBRACE -> strong
nested_italic: _NOT_LBRACE "{" ( TEXT | link ) "}" _NOT_RBRACE -> em

_NOT_LBRACE: /[^\{]/
_NOT_RBRACE: /[^\}]/

?link: a
     | footnote
     | wikipedia_link

a: "[" link_text "->" link_destination "]" -> a
link_text: TEXT -> text
link_destination: TEXT -> href
footnote: "[[" footnote_content "]]" -> footnote
footnote_content: TEXT -> content
wikipedia_link: "[?" wikipedia_query "]" -> a_wikipedia
wikipedia_query: TEXT -> query

// Pure text :
// - Never contains line breaks
// - Never contains curly braces
TEXT.0: /[^\r\n\{\}]/+
