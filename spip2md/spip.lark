// Flexible SPIP Markup grammar for Lark parser
start: _N* block ( _N+ block )* _N*

?block: HORIZONTAL_RULE -> horizontal_rule
      | heading
      | list
      | table
      | _block_tag
      | paragraph

HORIZONTAL_RULE: /----+/

?list: unordered_list
     | ordered_list

unordered_list: ( _UL list_item _N )+
ordered_list: ( _OL list_item _N )+
list_item: _inline{TEXT}+
_UL: /-\*|-[^#-]/
_OL: /-#/

table.1: ( _TBL_META table_metadata "||" _N )? ( table_row _N )+
table_metadata: table_title "|" table_description
table_title: _inline{TABLE_TEXT}
table_description: _inline{TABLE_TEXT}
table_row: ( _TBL table_cell )+ "|"
table_cell: _inline{TABLE_TEXT}
_TBL_META.1: "||"
_TBL.1: "|"

heading: _H _inline{MARKED_TEXT}+ "}}}"
_H: "{{{"

_block_tag.1: pair_block_tag
            | orphan_block_tag

pair_block_tag: _PAIR_TAG_ANGLE TAG_NAME ( "|" TAG_OPTION )* ">" start "</" _PURE_TEXT ">" -> tag
orphan_block_tag: _ORPHAN_TAG_ANGLE "/"? TAG_NAME ( "|" TAG_OPTION )* ">" -> orphan_tag

_PAIR_TAG_ANGLE: /\<(?=([0-9A-Za-z_:\/\-\.]+)(?:\|[0-9A-Za-z_:\/\-\.]+)*\>[\s\S]+\<\/\1\>)/
_ORPHAN_TAG_ANGLE: /\<(?=([0-9A-Za-z_:\/\-\.]+)(?:\|[0-9A-Za-z_:\/\-\.]+)*\>)(?![\s\S]+\<\/\1\>)/

paragraph.-1: ( _inline{TEXT} _N? )+

_inline{text}: _inline_tag
             | _link
             | emphasis
             | strong
             | text

_inline_tag.1: pair_inline_tag
             | orphan_inline_tag

pair_inline_tag: _PAIR_INLINE_TAG_ANGLE TAG_NAME ( "|" TAG_OPTION )* ">" paragraph ( "</" _PURE_TEXT ">" | _N ) -> tag
orphan_inline_tag: _INLINE_TAG_ANGLE "/"? TAG_NAME ( "|" TAG_OPTION )* ">" -> orphan_tag

_PAIR_INLINE_TAG_ANGLE: /\<(?=(?:quote|section|div|cadre|frame|code|poesie)(?:\|[0-9A-Za-z_:\/\-\.]+)*\>)/
_INLINE_TAG_ANGLE: /\<(?=([0-9A-Za-z_:\/\-\.]+)(?:\|[0-9A-Za-z_:\/\-\.]+)*\>)/

_link: footnote
     | wikilink
     | anchor

footnote: _FOOT HREF "]]"
wikilink: _WIKI HREF "]"
anchor: _A A_TEXT "->" HREF "]"
_FOOT: /\[\[/
_WIKI: /\[\?/
_A: /\[(?=[^\[\n\r]+->)/
HREF: _PURE_TEXT
A_TEXT: /[^\r\n\{]+?(?=->)/

strong: _B ( _inline{MARKED_TEXT} )+ ( "}}" | _N )
emphasis: _I ( _inline{MARKED_TEXT} )+ ( "}" | _N )
_B: /{{(?=[^\{])/
_I: /{(?=[^\{])/

TEXT.-1: / (?:[^\r\n\{\<](?![^\[\n\r]*->))+
         | (?:\<(?![0-9A-Za-z_:\/\-\.]+\>))+
         /x
TABLE_TEXT.-1: /(?:[^\|\r\n\{](?![^\[\n\r]*->))+/
MARKED_TEXT.-1: /(?:[^\}\r\n\{](?![^\[\n\r]*->))+/
TAG_NAME: _PURE_TEXT
TAG_OPTION: _PURE_TEXT

_N: /\r?\n/
_PURE_TEXT: /[0-9A-Za-z_:\/\-\.]+/
