// Flexible SPIP Markup grammar for Lark parser
start: _N* block ( _N+ block )* _N*

?block: paragraph
      | heading
      | list
      | table
      | tag
      | HORIZONTAL_RULE -> horizontal_rule

HORIZONTAL_RULE: /----+/

?list: unordered_list
     | ordered_list

unordered_list: ( _UL list_item _N )+
ordered_list: ( _OL list_item _N )+
list_item: _inline{TEXT}+
_UL: /-\*|-[^#-]/
_OL: /-#/

table: ( _TBL_META table_metadata "||" _N )? ( table_row _N )+
table_metadata: table_title "|" table_description
table_title: _inline{TABLE_TEXT}
table_description: _inline{TABLE_TEXT}
table_row: ( _TBL table_cell )+ "|"
table_cell: _inline{TABLE_TEXT}
_TBL_META: "||"
_TBL: "|"

heading: _H _inline{MARKED_TEXT}+ "}}}"
_H: "{{{"

paragraph.-1: ( _inline{TEXT} _N? )+

_inline{text}: text
             | emphasis
             | strong
             | anchor
             | tag

TEXT: /(?:[^\r\n\{](?![^\[\n\r]*->))+/
TABLE_TEXT: /[^\|\r\n\{]+/
MARKED_TEXT: /[^\}\r\n\{]+/

strong: _B ( _inline{MARKED_TEXT} )+ ( "}}" | _N )
emphasis: _I ( _inline{MARKED_TEXT} )+ ( "}" | _N )
_B: /{{(?=[^\{])/
_I: /{(?=[^\{])/

?anchor: anchor_footnote
       | anchor_wikipedia
       | anchor_normal -> anchor

anchor_footnote: _FOOT HREF "]]"
anchor_wikipedia: _WIKI HREF "]"
anchor_normal: _A A_TEXT "->" HREF "]"
_FOOT: /\[\[/
_WIKI: /\[\?/
_A: /\[(?=[^\[\n\r]+->)/
HREF: _PURE_TEXT
A_TEXT: /[^\r\n\{]+?(?=->)/

tag: end_tag
   | start_tag

end_tag: _ETAG TAG_NAME ( "|" TAG_OPTION )* ">"
start_tag: _STAG TAG_NAME ( "|" TAG_OPTION )* ">"
_STAG: "<"
_ETAG: "</"
TAG_NAME: _PURE_TEXT
TAG_OPTION: _PURE_TEXT

_N: /\r?\n/
_PURE_TEXT: /[0-9A-Za-z_:\/\-\.]+/
