// Flexible SPIP Markup grammar for Lark parser
start: _N* block ( _N+ block )+ _N*

?block: paragraph
      | heading
      | list
      | table
      | tag
      | HORIZONTAL_RULE -> horizontal_rule

HORIZONTAL_RULE: /----+/

?list: unordered_list
     | ordered_list

unordered_list: ( _UL list_item _N )+
ordered_list: ( _OL list_item _N )+
list_item: _inline
_UL: /-|-\*/
_OL: /-#/

table: ( _TBL_META table_metadata "||" _N )? ( table_row _N )+
table_metadata: table_title "|" table_description
table_title: _table_inline
table_description: _table_inline
table_row: ( _TBL table_cell )+ "|"
table_cell: _table_inline
_TBL_META: "||"
_TBL: "|"

heading: _H _markup_inline "}}}"
_H: "{{{"

paragraph.-1: ( _inline _N? )+

_inline: TEXT
       | emphasis
       | strong
       | anchor
       | tag

TEXT.-1: /[^\r\n]+/

_table_inline: TABLE_TEXT
             | emphasis
             | strong
             | anchor

TABLE_TEXT: /[^\r\n|]+(?=[\{\[\|])/

_markup_inline: MARKUP_TEXT
              | emphasis
              | strong
              | anchor
              | tag

MARKUP_TEXT.-1: /[^\r\n\}]+/

strong: _B ( _markup_inline )+ "}}"
emphasis: _I ( _markup_inline )+ "}"
_B: "{{"
_I: "{"

?anchor: anchor_footnote
       | anchor_wikipedia
       | anchor_normal -> anchor

anchor_footnote: _FOOT HREF "]]"
anchor_wikipedia: _WIKI HREF "]"
anchor_normal: _A A_TEXT "->" HREF "]"
_FOOT: "[["
_WIKI: "[?"
_A: /\[(?=[^\n\r]+->)/
HREF: /[^\r\n\]]+/
A_TEXT: /[^\r\n]+(?=->)/

tag: end_tag
   | start_tag

end_tag: _E_TAG TAG_TEXT ( "|" TAG_TEXT )* ">"
start_tag: _S_TAG TAG_TEXT ( "|" TAG_TEXT )* ">"
_S_TAG: "<"
_E_TAG: "</"
TAG_TEXT: /[\r\n\|\>]+/

_N: /\r?\n/
